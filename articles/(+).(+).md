### Definitioner

    (+) === \x -> (\y -> x + y)
    (.) === \f -> (\g -> (\x ->  f (g x)))



### Förkortning

    (+).(+)

=> prefix-syntax =>

    (.) (+) (+)

=> expandera definitionerna =>

    (\f -> (\g -> (\x ->  f (g x))))     (\x -> (\y -> x + y))     (\x -> (\y -> x + y))

=> betareducera "`f`" (alfakonvertera `x` till `z`) =>

    (\g -> (\x ->  (\z -> (\y -> z + y)) (g x)))        (\x -> (\y -> x + y))

=> betareducera "`z`" =>

    (\g -> (\x ->  (\y -> (g x) + y)))        (\x -> (\y -> x + y))
     
=> betareducera "`g`" (alfakonvertera `x` till `z`, `y` till `w`) =>

    (\x -> (\y -> ((\z -> (\w -> z + w)) x) + y))

=> betareducera "`z`" =>

    (\x -> (\y -> (\w -> x + w) + y))

Vilket är slutförkortat.



### Relevanta typer

Allting som ingår i en addition måste vara en instans av `Num`. Detta innebär att i uttrycket

    (+).(+) === (\x -> (\y -> (\w -> x + w) + y))

så gäller att,

    x, w :: Num a                       eftersom de adderas med varandra
    (\w -> x + w) :: Num (a -> a)       eftersom det är en funktion som blir adderad
    y :: Num (a -> a)                   eftersom den blir adderad med en funktion
    (\w -> x + w) + y :: Num (a -> a)   eftersom addition är (Num a) => a -> a -> a

Hence, om man missbrukar notation lite,

    (+).(+) :: Num a -> Num (a -> a) -> Num (a -> a)

vilket läses som, "uttrycket tar ett numeriskt argument och ett argument som är
både numeriskt och en funktion. Resultatet blir ett värde som är numeriskt och
en funktion."

Det bör däremot skrivas med typklasserna innan, och då blir det

    (+).(+) :: (Num a, Num (a -> a)) => a -> (a -> a) -> (a -> a)

eller, som är precis samma sak och det GHC skriver,

    (+).(+) :: (Num a, Num (a -> a)) => a -> (a -> a) -> a -> a



### Tolkning

För att uttrycket på något sätt ska "make sense" så krävs det att man uppfinner en
vettig `Num`-instans för någon slags funktion `a -> a`. `Num`-klassen är definierad som:

    class  (Eq a, Show a) => Num a  where
        (+), (-), (*)    :: a -> a -> a
        negate           :: a -> a
        abs, signum      :: a -> a
        fromInteger      :: Integer -> a

        -- Minimal complete definition:
        --      All, except negate or (-)

Till att börja med kan man definiera `(+)` för funktioner:

    instance (Num a) => Num (a -> a) where
      f + g = \x -> f x + g x

och ur det följer `(-)` ganska naturligt, liksom `(*)`.

      f - g = \x -> f x - g x
      f * g = \x -> f x * g x

På samma vis kan man bygga `abs` och `signum`.

      abs f = abs . f
      signum f = signum . f

Medan `fromInteger` blir lurig. Jag kan tänka mig flera sätt att konvertera `Integer`s
till funktioner, men det mest logiska är kanske

      fromInteger = const . fromInteger

Instansen är lite™ buggig (eller så förstår jag den inte helt) och kräver
`-XFlexibleInstances` till GHC, men den gör att man kan evaluera `(+).(+)` ned
till ett numeriskt värde.

    > ((+).(+)) 5 (2^) 9
    526

...vilket är riktigt jävla weird. Och coolt.